# Issue 30081: Dot and cross products along a differentiable map

Issue created by migration from https://trac.sagemath.org/ticket/30318

Original creator: egourgoulhon

Original creation time: 2020-08-07 23:54:38

CC:  tscrim @mjungmath mkoeppe

Keywords: dot product, cross product, curve

This ticket makes the methods `dot_product()`, `cross_product()` and `norm()` of class `VectorField` work for vector fields defined along a differentiable map, the codomain of which is a Riemannian manifold. 
Previously, these methods worked only for vector fields _on_ a Riemannian manifold, i.e. along the identity map. 
An important subcase is of course that of a curve in a Riemannian manifold.

For instance, considering a helix parametrized by its arc length:

```
sage: E.<x,y,z> = EuclideanSpace()
sage: R.<s> = RealLine()
sage: C = E.curve((2*cos(s/3), 2*sin(s/3), sqrt(5)*s/3), (s, 0, 6*pi),
....:             name='C')
```

we have now

```
sage: T = C.tangent_vector_field()
sage: T.display()
C' = -2/3*sin(1/3*s) e_x + 2/3*cos(1/3*s) e_y + 1/3*sqrt(5) e_z
sage: norm(T)
Scalar field |C'| on the Real interval (0, 6*pi)
sage: norm(T).expr()
1
```

Introducing the unit normal vector N via the derivative of T:

```
sage: I = C.domain()
sage: Tp = I.vector_field([diff(T[i], s) for i in E.irange()], dest_map=C,
....:                     name="T'")
sage: N = Tp / norm(Tp)   
```

we get the binormal vector as the cross product of T and N:

```
sage: B = T.cross_product(N)
sage: B
Vector field along the Real interval (0, 6*pi) with values on the
 Euclidean space E^3
sage: B.display()
1/3*sqrt(5)*sin(1/3*s) e_x - 1/3*sqrt(5)*cos(1/3*s) e_y + 2/3 e_z
```

We can then form the Frenet-Serret frame:

```
sage: FS = I.vector_frame(('T', 'N', 'B'), (T, N, B),
....:                     symbol_dual=('t', 'n', 'b'))
sage: FS
Vector frame ((0, 6*pi), (T,N,B)) with values on the Euclidean space E^3
```

and check that it is orthonormal:

```
sage: [[u.dot(v).expr() for v in FS] for u in FS]
[[1, 0, 0], [0, 1, 0], [0, 0, 1]]
```

The Frenet-Serret formulas are obtained as:

```
sage: Np = I.vector_field([diff(N[i], s) for i in E.irange()],
....:                     dest_map=C, name="N'")
sage: Bp = I.vector_field([diff(B[i], s) for i in E.irange()],
....:                     dest_map=C, name="B'")
sage: for v in (Tp, Np, Bp):
....:     v.display(FS)
....:
T' = 2/9 N
N' = -2/9 T + 1/9*sqrt(5) B
B' = -1/9*sqrt(5) N
```





---

Comment by egourgoulhon created at 2020-08-07 23:57:46

New commits:


---

Comment by egourgoulhon created at 2020-08-07 23:59:36

Changing status from new to needs_review.


---

Comment by tscrim created at 2020-08-08 00:20:48

Thank you for this. The code looks good. One minor doctest tweak:

```diff
-sage: [[u.dot(v).expr() for v in FS] for u in FS]
+sage: matrix([[u.dot(v).expr() for v in FS] for u in FS])
```

as I think the output is a little easier to read and to reflect that it is the Gram matrix of dot product as a bilinear form.

Once changed (or you say my idea is stupid) and a green patchbot, then positive review.


---

Comment by egourgoulhon created at 2020-08-08 11:36:33

Changing status from needs_review to needs_work.


---

Comment by egourgoulhon created at 2020-08-08 11:36:33

There are doctest errors in `pseudo_riemannian_submanifold.py`.


---

Comment by egourgoulhon created at 2020-08-08 11:37:47

Replying to [comment:3 tscrim]:
> Thank you for this. The code looks good. One minor doctest tweak:
> {{{#!diff
> -sage: [[u.dot(v).expr() for v in FS] for u in FS]
> +sage: matrix([[u.dot(v).expr() for v in FS] for u in FS])
> }}}
> as I think the output is a little easier to read and to reflect that it is the Gram matrix of dot product as a bilinear form.

Thanks for the suggestion; I'll perform the change.


---

Comment by git created at 2020-08-08 19:21:48

Branch pushed to git repo; I updated commit sha1. New commits:


---

Comment by egourgoulhon created at 2020-08-08 19:26:02

The latest version corrects the doctest errors, takes into account comment:5 and extends the capabilities to the dot/cross product between a vector field along a diff map and a vector field defined on the codomain of the diff map.


---

Comment by egourgoulhon created at 2020-08-08 19:26:02

Changing status from needs_work to needs_review.


---

Comment by tscrim created at 2020-08-08 23:44:12

Thank you. LGTM.


---

Comment by tscrim created at 2020-08-08 23:44:12

Changing status from needs_review to positive_review.


---

Comment by egourgoulhon created at 2020-08-09 09:53:37

Thank you for the review!


---

Comment by vbraun created at 2020-08-10 18:50:03

Resolution: fixed
