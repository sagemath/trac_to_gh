# Issue 15460: Upgrade MPC to latest upstream

Issue created by migration from Trac.

Original creator: jdemeyer

Original creation time: 2014-01-19 22:47:03

CC:  zimmerma thevenyp

Drop-in upgrade.


---

Comment by jdemeyer created at 2014-01-19 23:02:21

Changing status from new to needs_review.


---

Comment by jdemeyer created at 2014-01-19 23:04:48

New commits:


---

Comment by fbissey created at 2014-01-20 21:58:40

Is bz2 rather than gz if it is the only thing provided by upstream a hard requirement?


---

Comment by jdemeyer created at 2014-01-21 09:25:40

Given that every other package in Sage (except for `bzip2` obviously) uses `.bz2`, I decided to recompress to `.bz2`. But I personally don't care much...


---

Comment by fbissey created at 2014-01-21 10:00:28

I guess my concern is can the sage build system cope with gz. I might be a purist but I think we should as much as possible include stuff from upstream as is. It make things easier to include and baring incident like flint 2.4.1 we have the checksum from the upstream tarball rather than the one produced by a sage dev. This is stuff for sage-devel really.

I have otherwise no concern with this upgrade.


---

Comment by jdemeyer created at 2014-01-21 10:03:06

Replying to [comment:6 fbissey]:
> I guess my concern is can the sage build system cope with gz.
Yes, it can.


---

Comment by jdemeyer created at 2014-01-21 10:38:25

Fran√ßois, for me it's fine to take the pure upstream `.gz`. It only requires changing the `tarball=` line in `build/pkgs/mpc/checksums.ini`.


---

Comment by fbissey created at 2014-01-21 10:41:07

While I raised the point I suddenly feel that it is not my place to make that decision. There should be guidelines agreed with other in this case.


---

Comment by jdemeyer created at 2014-01-24 08:51:11

Changing status from needs_review to needs_work.


---

Comment by git created at 2014-01-24 08:58:51

Branch pushed to git repo; I updated commit sha1. This was a forced push. New commits:


---

Comment by jdemeyer created at 2014-01-24 09:05:23

Changing status from needs_work to needs_review.


---

Comment by fbissey created at 2014-01-24 09:21:26

OK I said I would review packages :) The sheer reduction of patches alone justify this one.


---

Comment by fbissey created at 2014-01-24 09:21:26

Changing status from needs_review to positive_review.


---

Comment by vbraun created at 2014-02-01 19:39:16

Resolution: fixed


---

Comment by leif created at 2014-04-25 14:38:16

Just for the record:

`configure` is still slightly broken w.r.t. `CC` and `CFLAGS`, since the "hack" currently ignores the setting of `CPP` (and instead tries `/lib/cpp` with `CPPFLAGS` first, which may not work, resulting in empty `CC` and `CFLAGS` settings from `gmp.h`, such that finally `gcc` is used, which may of course also fail).

I'll _one day_<sup>TM</sup> report this upstream.


---

Comment by leif created at 2014-04-25 14:53:27

P.S.:  From `m4/mpc.m4`:


```sh
#
# SYNOPSIS
#
#
# MPC_GMP_CC_CFLAGS
#
# DESCRIPTION
#
# Checks if CC and CFLAGS can be extracted from gmp.h
# essentially copied from mpfr
#
AC_DEFUN([MPC_GMP_CC_CFLAGS], [
   AC_MSG_CHECKING(for CC and CFLAGS in gmp.h)
   # AC_PROG_CPP triggers the search for a C compiler; use hack instead
   for cpp in /lib/cpp gcc cc c99
   do
      test $cpp = /lib/cpp || cpp="$cpp -E"
      echo foo > conftest.c
      if $cpp $CPPFLAGS conftest.c > /dev/null 2> /dev/null ; then
         # Get CC
         echo "#include \"gmp.h\"" >  conftest.c
         echo "MPFR_OPTION __GMP_CC"           >> conftest.c
         GMP_CC=`$cpp $CPPFLAGS conftest.c 2> /dev/null | $EGREP MPFR_OPTION | $SED -e 's/MPFR_OPTION //g;s/ *" *//g'`
         # Get CFLAGS
         echo "#include \"gmp.h\"" >  conftest.c
         echo "MPFR_OPTION __GMP_CFLAGS"           >> conftest.c
         GMP_CFLAGS=`$cpp $CPPFLAGS conftest.c 2> /dev/null | $EGREP MPFR_OPTION | $SED -e 's/MPFR_OPTION //g;s/ *" *//g'`
         break
      fi
   done

   if test "x$GMP_CFLAGS" = "x__GMP_CFLAGS" -o "x$GMP_CC" = "x__GMP_CC" ; then
      AC_MSG_RESULT(no)
      GMP_CC=
      GMP_CFLAGS=
   else
      AC_MSG_RESULT(yes [CC=$GMP_CC CFLAGS=$GMP_CFLAGS])
   fi

   # Check for validity of CC and CFLAGS obtained from gmp.h
   if test -n "$GMP_CC$GMP_CFLAGS" ; then
      AC_MSG_CHECKING(for CC=$GMP_CC and CFLAGS=$GMP_CFLAGS)
      echo "int main (void) { return 0; }" > conftest.c
      if $GMP_CC $GMP_CFLAGS -o conftest conftest.c 2> /dev/null ; then
         AC_MSG_RESULT(yes)
         CC=$GMP_CC
         CFLAGS=$GMP_CFLAGS
      else
         AC_MSG_RESULT(no)
      fi
   fi

   rm -f conftest*
])
```


I don't exactly recall what went wrong, but `configure` gave me something like


```
checking for CC and CFLAGS in gmp.h ... yes CC= CFLAGS=
```


and finally `gcc` was used (instead of `$CC`, while the same setting was recorded in `gmp.h`).


---

Comment by leif created at 2014-04-25 15:53:00

Ah, the problem arises when _none of_ `/lib/cpp gcc cc c99` work (with `$CPPFLAGS`), since then `GMP_CC` and `GMP_CFLAGS` will remain *empty*, and / but

```sh
test "x$GMP_CFLAGS" = "x__GMP_CFLAGS" -o "x$GMP_CC" = "x__GMP_CC"
```

yields `false`, and

```sh
      AC_MSG_RESULT(yes [CC=$GMP_CC CFLAGS=$GMP_CFLAGS])
```

is executed, but *not* what follows (since `test -n "$GMP_CC$GMP_CFLAGS"` yields `false`):

```sh
   # Check for validity of CC and CFLAGS obtained from gmp.h
   if test -n "$GMP_CC$GMP_CFLAGS" ; then
      AC_MSG_CHECKING(for CC=$GMP_CC and CFLAGS=$GMP_CFLAGS)
      echo "int main (void) { return 0; }" > conftest.c
      if $GMP_CC $GMP_CFLAGS -o conftest conftest.c 2> /dev/null ; then
         AC_MSG_RESULT(yes)
         CC=$GMP_CC
         CFLAGS=$GMP_CFLAGS
      else
         AC_MSG_RESULT(no)
      fi
   fi
```





I ended up with

```
...
checking for egrep... /bin/grep -E
checking for a sed that does not truncate output... /bin/sed
checking for CC and CFLAGS in gmp.h... yes CC= CFLAGS=
checking for gcc... gcc
checking whether the C compiler works... no
configure: error: in `$SAGE_BUILD_DIR/mpc-1.0.p0/src':
configure: error: C compiler cannot create executables
See `config.log' for more details
Error configuring MPC.

real	0m1.737s
user	0m0.032s
sys	0m0.068s
************************************************************************
Error installing package mpc-1.0.p0
************************************************************************
```

(because `gcc` didn't work with `$CFLAGS` either, while `$CC` or `__GMP_CC` from `gmp.h` would have worked).

(In the mpc-1.0.p0 spkg, we already use[d] the related [upstream patch](https://gforge.inria.fr/tracker/?func=detail&group_id=131&aid=14669&atid=607) (included in MPC 1.0.1) fixing [a slightly different issue](http://trac.sagemath.org/ticket/13290#comment:13) with `GMP_CC` and `GMP_CFLAGS`, so the same holds for MPC 1.0.2.)


---

Comment by jdemeyer created at 2014-05-09 11:53:47

I'm posting an answer from Andreas Enge, MPC developer:


Does the following patch solve the problem for you?


```diff
--- m4/mpc.m4   (revision 1450)
+++ m4/mpc.m4   (working copy)
`@``@` -1,6 +1,6 `@``@`
 # mpc.m4
 #
-# Copyright (C) 2008, 2009, 2010, 2011, 2012 INRIA
+# Copyright (C) 2008, 2009, 2010, 2011, 2012, 2014 INRIA
 #
 # This file is part of GNU MPC.
 #
`@``@` -127,9 +127,12 `@``@`
 AC_DEFUN([MPC_GMP_CC_CFLAGS], [
    AC_MSG_CHECKING(for CC and CFLAGS in gmp.h)
    # AC_PROG_CPP triggers the search for a C compiler; use hack instead
-   for cpp in /lib/cpp gcc cc c99
+   for cpp in $CPP cpp gcc /lib/cpp cc c99
    do
-      test $cpp = /lib/cpp || cpp="$cpp -E"
+     case $cpp in
+       *cpp*) ;;
+       *) cpp="$cpp -E" ;;
+     esac
       echo foo > conftest.c
       if $cpp $CPPFLAGS conftest.c > /dev/null 2> /dev/null ; then
          # Get CC
```


It brings things a bit closer to mpfr and adds `$CPP` to the list of possible
preprocessors.

If yes, I can push it to the mpc trunk.

Thanks in advance,

Andreas


---

Comment by leif created at 2014-05-09 12:54:55

Replied off-trac:

```
...
Should work for me (will test later),
but $cpp should be quoted in 'case $cpp in ...',
and $CPP in the 'for' statement.

You could also go with

    for cpp in "$CPP" cpp "gcc -E" /lib/cpp "cc -E" "c99 -E"

and drop adding '-E' below.
...
```



---

Comment by leif created at 2014-05-09 13:16:32

P.S.:

```sh
   if test "x$GMP_CFLAGS" = "x__GMP_CFLAGS" -o "x$GMP_CC" = "x__GMP_CC" ; then
      AC_MSG_RESULT(no)
      GMP_CC=
      GMP_CFLAGS=
   else
      AC_MSG_RESULT(yes [CC=$GMP_CC CFLAGS=$GMP_CFLAGS])
   fi
```

could be changed to something like

```sh
   if test -z "$GMP_CFLAGS$GMP_CC" ; then
      AC_MSG_RESULT([no, no working preprocessor found; CPP=$CPP CPPFLAGS=$CPPFLAGS])
   elif test "x$GMP_CFLAGS" = "x__GMP_CFLAGS" -o "x$GMP_CC" = "x__GMP_CC" ; then
      AC_MSG_RESULT(no)
      GMP_CC=
      GMP_CFLAGS=
   else
      AC_MSG_RESULT(yes [CC=$GMP_CC CFLAGS=$GMP_CFLAGS])
   fi
```



---

Comment by leif created at 2014-05-12 16:13:44

The GMP `CC` / `CFLAGS` issue with `cpp` (or rather `CPPFLAGS` not working with the ones from the list) is now [fixed upstream](https://gforge.inria.fr/scm/viewvc.php?view=rev&root=mpc&revision=1451) (MPC 1.1 branch); cf. [https://gforge.inria.fr/tracker/index.php?func=detail&aid=17410&group_id=131&atid=607](https://gforge.inria.fr/tracker/index.php?func=detail&aid=17410&group_id=131&atid=607).

I'll perhaps open a follow-up ticket including that upstream patch, but it's a minor issue anyway (which apparently only hits _me_ on occasion).
