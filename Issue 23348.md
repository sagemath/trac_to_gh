# Issue 23348: Gauss sums for geenral finite fields

Issue created by migration from Trac.

Original creator: chapoton

Original creation time: 2017-08-05 12:47:27

CC:  cremona wuthrich roed jdemeyer

Currently Gauss sums are available as method of Dirichlet Groups.

Here is code for an implementation for general finite fields.

Where to put it ? As a method of finite fields ?


```
def gauss_sum(m, p, f):
    r"""
    Return the Gauss sums for a general finite field `\GF(q)`.

    INPUT:
    
    - ``m`` -- integer, the power of the generator of character group

    - ``p`` -- a prime number

    - ``f`` -- an integer, such that `q = p ^ f`

    OUTPUT:

    an element of the universal cyclotomic field

    The character used for the Gauss sum is the `m`-th power of the
    character `\chi` (a generator of the character group) that satisfies

    .. MATH::

        \chi(x) = e^{2 i \pi / (q-1)}
   
    where `x` is the multiplicative generator of `\GF(q)` that is
    return by ``GF(q).multiplicative_generator()``.

    EXAMPLES::

        sage: L = [general_gauss_sum(i,5,1) for i in range(5)]; L
        [-1,
         E(20)^4 + E(20)^13 - E(20)^16 - E(20)^17,
         E(5) - E(5)^2 - E(5)^3 + E(5)^4,
         E(20)^4 - E(20)^13 - E(20)^16 + E(20)^17,
         -1]
        sage: [g*g.conjugate() for g in L]
        [1, 5, 5, 5, 1]

        sage: g = general_gauss_sum(4,11,2)
        sage: g*g.conjugate()
        121
    """
    q = p ** f
    Fq = GF(q, 'a')
    gen = Fq.multiplicative_generator()
    C = CyclicPermutationGroup(q - 1)
    UCF = UniversalCyclotomicField()
    zeta_p = UCF.gen(p)
    zeta_q = UCF.gen(q - 1) ** m

    resu = UCF.zero()
    gen_power = Fq.one()
    zq_power = UCF.one()
    for k in range(q - 1):
        resu += zq_power * zeta_p**(gen_power.trace())
        gen_power *= gen
        zq_power *= zeta_q
    return resu
```



---

Comment by chapoton created at 2017-08-05 12:48:10

Changing status from new to needs_info.


---

Comment by roed created at 2017-08-06 04:47:49

We had a similar problem with p-adic gauss sums recently, in #23456 (as I'm sure you know, since you finished reviewing it).  The solution there was as a function in `sage/rings/padics/misc.py`, but I'm not very happy with that, since it's not very discoverable.

Would sage.arith.misc be an appropriate place, and then imported into the global namespace?  We could have a gauss_sum function there that dispatched to various kinds of gauss sums....


---

Comment by chapoton created at 2017-08-22 07:20:48

New commits:


---

Comment by chapoton created at 2017-08-22 07:20:48

Changing status from needs_info to needs_review.


---

Comment by vdelecroix created at 2017-08-22 10:26:45

would better be in `sage/arith/`.


---

Comment by chapoton created at 2017-08-22 11:04:21

where precisely ?


---

Comment by vdelecroix created at 2017-08-22 11:08:23

sorry `sage/arith/misc.py` is fine.


---

Comment by vdelecroix created at 2017-08-22 11:22:57

In the finite field construction, there is no need to give a name for the generator.

It would be useful to be able to choose the ring `QQbar`, `UCF`, `ComplexField(n)`, ... In order to make it work you just have to replace the two calls to `gen` into calls to `zeta`

```
sage: CC.zeta(5)
0.309016994374947 + 0.951056516295154*I
sage: UniversalCyclotomicField().zeta(5)
E(5)
sage: QQbar.zeta(5)
0.3090169943749474? + 0.9510565162951536?*I
```


On a related note, I think that it would be good to have a common entry point for boths Gauss sums (the p-adic one and the one in this ticket)? What about something along

```
def gauss_sum(a, p, f, ring=None, **extra_kwds):
    if ring is None:
        ring = UniversalCyclotomicField()
    ...
```

In that case, I would actually rename the p-adic `gauss_sum` into `padic_gauss_sum`.


---

Comment by git created at 2017-08-22 12:21:47

Branch pushed to git repo; I updated commit sha1. New commits:


---

Comment by chapoton created at 2017-08-22 12:26:25

I allowed more general rings in the new function introduced here. Concerning the common entry point, I do not know what would be the best thing to do. Maybe this can be left for another ticket.


---

Comment by chapoton created at 2017-08-22 19:36:23

green bot.


---

Comment by vdelecroix created at 2017-08-22 19:44:48

1. The silent dependency in the choice of multiplicative generator (through the call to `.multiplicative_generator()`) is bad. The way it works with `DirichletCharacter` is much better because you explicitely specify it.

2. Would be good to have crossed examples such as this one

```
sage: D = DirichletGroup(7, QQbar)
sage: for i in range(6):
....:     print D[i].gauss_sum()
....:     print sage.arith.misc.gauss_sum(i, 7, 1, ring=QQbar)
-1
-1
-2.440133358345538? + 1.022618791871794?*I
-2.440133358345538? + 1.022618791871794?*I
2.370469405576201? - 1.175106291884787?*I
2.370469405576201? - 1.175106291884787?*I
0.?e-17 + 2.645751311064591?*I
0.?e-17 + 2.645751311064591?*I
2.370469405576201? + 1.175106291884787?*I
2.370469405576201? + 1.175106291884787?*I
2.440133358345538? + 1.022618791871794?*I
2.440133358345538? + 1.022618791871794?*I
```

(by chance we get the same ordering!?)


3. Putting appart p-adic, this ticket proposes to have a *third* implementation of gauss sums beyond the 2 already there in `DirichletGroup`... I am not sure there is a need for one more implementation. Refactorization is more than expected at this point.


---

Comment by chapoton created at 2017-08-22 19:59:52

Concerning point 3, the new implementation can be obtained from the DirichletGroup one if and only if the finite field has cardinality a prime number. Outside of prime numbers the Dirichlet group implementation returns something else (which is probably of no interest, in fact).

Concerning point 2, I can add doctests, indeed.

Concerning point 1, the choice of multiplicative generator is explained in the doc.


---

Comment by git created at 2017-08-22 20:05:28

Branch pushed to git repo; I updated commit sha1. New commits:


---

Comment by vdelecroix created at 2017-08-22 20:12:33

Replying to [comment:15 chapoton]:
> Concerning point 3, the new implementation can be obtained from the DirichletGroup one if and only if the finite field has cardinality a prime number. Outside of prime numbers the Dirichlet group implementation returns something else (which is probably of no interest, in fact).

Why is the implementation in `DirichletGroup` still there then?
 
> Concerning point 2, I can add doctests, indeed.
> 
> Concerning point 1, the choice of multiplicative generator is explained in the doc.

This is not helpful at all. If I have a multiplicative character, with your new function I would have to make a logarithm in order to get my Gauss sum...


---

Comment by vdelecroix created at 2017-08-22 21:40:24

Moreover, introducing a generic `gauss_sum` should really be generic, ie taking as input
- a finite commutative ring `R` and a codomain that embedds enough root of unity like `CC`
- a multiplicative character `chi: (R^x, *) \to (CC^x, *)`
- an additive character `psi: (R, +) \to (CC^x, *)`
and return the Gauss sum `sum_{a in R^x} chi(a) psi(a)`


---

Comment by vdelecroix created at 2017-08-22 21:40:46

Changing keywords from "" to "days88, IMA coding sprints".


---

Comment by vdelecroix created at 2017-08-22 21:40:46

Changing status from needs_review to needs_work.


---

Comment by git created at 2017-08-23 09:28:48

Branch pushed to git repo; I updated commit sha1. New commits:


---

Comment by chapoton created at 2017-08-23 12:30:17

I have made so that the character is now an input, given by its value on the multiplicative generator of the group of invertible elements.

I am not going to remove the method from `DirichletGroup`. It may be used by somebody.

I would rather not go to full generality, as long as it has no specific and concrete use. The new function, in its current form, is needed for #23671.
----
New commits:


---

Comment by vdelecroix created at 2017-08-23 13:38:57

I am sorry to bother you so much Frédéric. You are introducing a function in the global namespace with a very specific syntax that a user (like me) might want to see much more general. In your version, you are forcing the additive character used in the Gauss sum to be given by the trace, but it is not the only one additive character of `GF(q)`. The input of a Gauss sum should be a pair `(multiplicative_character, additive_character)`. If a global `gauss_sum` is introduced it should support this generality.

A possible solution is to replace the input of `p` and `f` by `GF(q)`, make the function as `gauss_sum(chi, psi=None)` and allow the following inputs
- `gauss_sum(chi, psi)`: the Gauss sum of the multiplicative character `chi` and additive character `psi` (would not be currently implemented)
- `gauss_sum(chi)`: `chi` a Dirichlet character
- `gauss_sum(char_value, finite_field)`: the Gauss sum of the multiplicative character `chi` as you have in the current version and `psi` being given by the trace

What do you think?


---

Comment by chapoton created at 2017-08-23 13:50:32

Let me thank you for looking at my ticket. I understand that a review is not necessarily a joyful process. I am trying to remain perfectly zen.

Note that I am adding no function to the global namespace. This is only a specialized function, to be used in a specific context.

I understand that you would like to have a fully general function. I did not intend to spend a lot of time on this simple ticket, and would prefer not to.

I can replace the input (p,f) by the input GF(q), of course. I was thinking at the beginning to have this as a method of finite fields.


---

Comment by vdelecroix created at 2017-08-23 13:58:02

I will be happy with `gauss_sum(char_value, finite_field)` that would be easily turned into a general `gauss_sum(chi, psi)`. Can you add a to the doc

```
For more info on Gauss sum see :wikipedia:`Gauss_sum`.

.. TODO::

    Implement general Gauss sums with a pair ``(multiplicative_character, additive_character)``
```



---

Comment by git created at 2017-08-23 14:43:53

Branch pushed to git repo; I updated commit sha1. New commits:


---

Comment by chapoton created at 2017-08-23 14:44:44

Thanks. Here is a version that may suit you, hopefuly.


---

Comment by vdelecroix created at 2017-08-23 15:30:00

You should put the definition of what is meant by Gauss sum over a finite field (or update wikipedia ;-).


---

Comment by git created at 2017-08-23 17:00:47

Branch pushed to git repo; I updated commit sha1. New commits:


---

Comment by git created at 2017-08-23 18:00:31

Branch pushed to git repo; I updated commit sha1. New commits:


---

Comment by chapoton created at 2017-08-23 18:03:01

ok, doc added and checked.


---

Comment by chapoton created at 2017-08-23 18:14:26

Changing status from needs_work to needs_review.


---

Comment by vdelecroix created at 2017-08-23 19:04:12

Thanks for your patience!


---

Comment by vdelecroix created at 2017-08-23 19:04:12

Changing status from needs_review to positive_review.


---

Comment by vbraun created at 2017-09-04 06:10:49

Resolution: fixed
